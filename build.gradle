buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/maven' }
        maven { url = 'https://plugins.gradle.org/m2/' }
        maven { url = 'https://maven.parchmentmc.org' }

        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath group: 'net.minecraftforge.gradle',           name: 'ForgeGradle', version: '5.1+',          changing: true
        classpath group: 'org.spongepowered',                   name: 'mixingradle', version: '0.7-SNAPSHOT'
        classpath group: 'gradle.plugin.com.modrinth.minotaur', name: 'Minotaur',    version: '1.2.1'
        classpath group: 'gradle.plugin.com.matthewprenger',    name: 'CurseGradle', version: '1.4.0'
        classpath 'org.parchmentmc:librarian:1.+'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'

ext.config = parseConfig(file('build.properties'))

version = config.VERSION
group = "${config.GROUP}.${config.ARTIFACT}"
archivesBaseName = "${config.ARCHIVES_BASE_NAME}-${config.MINECRAFT_VERSION}"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: config.MAPPINGS_CHANNEL, version: config.MAPPINGS_VERSION

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            //arg '-mixin.config=everydesc.mixins.json'
            property 'forge.logging.markers', 'SCAN,REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            mods {
                everydesc {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            //arg '-mixin.config=everydesc.mixins.json'
            property 'forge.logging.markers', 'SCAN,REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            mods {
                everydesc {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'everydesc', '--all', '--output', file('src/generated/resources/')
            mods {
                everydesc {
                    source sourceSets.main
                }
            }
        }
    }
}

//mixin {
//    add sourceSets.main, 'everydesc.refmap.json'
//    config 'shieldexp.mixins.json'
//
//    debug.verbose = true
//    debug.export = true
//    dumpTargetOnFailure = true
//}

repositories {
    maven { url = 'https://repo.spongepowered.org/maven/' }

    jcenter()
    mavenCentral()
}

dependencies {
    minecraft "net.minecraftforge:forge:${config.MINECRAFT_VERSION}-${config.FORGE_VERSION}"
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

jar {
    manifest {
        attributes([
                'Specification-Title'   : config.TITLE,
                'Specification-Vendor'  : config.AUTHOR,
                'Specification-Version' : '1',
                'Implementation-Title'  : config.TITLE,
                'Implementation-Version': config.VERSION,
                'Implementation-Vendor' : config.AUTHOR,
                //'MixinConfigs'          : 'everydesc.mixins.json',
        ])
    }
}

jar.finalizedBy('reobfJar')

//tasks.build.dependsOn sourcesJar

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = config.GROUP
            artifactId = config.ARTIFACT
            artifact jar
//            artifact sourcesJar
            version "${config.VERSION}-${config.MINECRAFT_VERSION}"

            pom {
                name = 'Everything Descriptions'
                url = 'https://infernalstudios.org/everydesc'
                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/infernalexp/Everything-Descriptions.git'
                    url = 'https://github.com/infernalexp/Everything-Descriptions'
                }
            }
        }
    }
    repositories {
        maven {
            url 'https://maven.infernalstudios.org/releases'

            credentials {
                username = System.getenv('MAVEN_USER')
                password = System.getenv('MAVEN_TOKEN')
            }

            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}

task publishModrinth() {
    println('Modrinth publishing is not setup yet!')
}

curseforge {
    apiKey = System.getenv('CURSEFORGE') ?: 'If you\'re seeing this, you haven\'t set your "CURSEFORGE" environment variable'

    project {
        id = config.CURSEFORGE_ID
        changelog = 'No changelog specified.'
        releaseType = 'alpha'
        addGameVersion config.MINECRAFT_VERSION
        mainArtifact (jar) {
            displayName = "Everything Descriptions Alpha ${project.version}"
        }
//        addArtifact sourcesJar
    }
}

if (System.getProperty("idea.sync.active") == "true") {
    afterEvaluate {
        tasks.withType(JavaCompile).all {
            it.options.annotationProcessorPath = files()
        }
    }
}

def parseConfig(File config) {
    config.withReader {
        def prop = new Properties()
        prop.load(it)
        return (new ConfigSlurper().parse(prop))
    }
}